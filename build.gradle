repositories {
    mavenCentral()
}

allprojects {
    ext {
        reactorVersion = '3.1.8.RELEASE'
        hikariVersion = '2.7.8'
        hibernateVersion = '5.3.6.Final'
        fluentHibernateVersion = '0.3.1'
        junitJupiterVersion = '5.2.0'
        lombokVersion = '1.16.20'
        hsqldbVersion = '2.4.0'
        mockitoJupiterVersion = '2.21.0'
        jacocoVersion = '2.17.0'
    }
}

subprojects {
    version = '0.0.1'
    group 'com.ibm.reactivecomponents'
    plugins.withId('java') {
        project.apply plugin: 'checkstyle'
        project.apply plugin: 'jacoco'
        project.apply plugin: 'maven'
    }

    plugins.withId('jacoco') {

        jacocoTestReport {
            reports {
                html.enabled = true
            }
        }

        jacocoTestCoverageVerification {
            violationRules {
                rule {
                    limit {
                        minimum = 0.95
                    }
                }
                failOnViolation true
            }
        }
        jacocoTestCoverageVerification.mustRunAfter test
        jacocoTestReport.mustRunAfter test

        check.dependsOn jacocoTestCoverageVerification
    }

    task lint {
        description 'Linting ...'
    }

    plugins.withId('checkstyle') {
        checkstyle {
            maxErrors 0
            ignoreFailures false
            configFile file("$rootProject.projectDir/config/checkstyle/checkstyle.xml")
            toolVersion "8.8"

        }

        lint.dependsOn checkstyleMain, checkstyleTest

        tasks.withType(Checkstyle).each { checkstyleTask ->
            checkstyleTask.doLast {
                reports.all { report ->
                    def outputFile = report.destination
                    if (outputFile.exists() && outputFile.text.contains("<error ")) {
                        throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
                    }
                }
            }
        }
    }

    if (project.hasProperty('SONATYPE_USERNAME')) {
        signing {
            sign configurations.archives
        }

        uploadArchives {
            repositories {
                // see: http://central.sonatype.org/pages/gradle.html
                mavenDeployer {
                    beforeDeployment {
                        MavenDeployment deployment -> signing.signPom(deployment)
                    }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: SONATYPE_USERNAME, password: SONATYPE_PASSWORD)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: SONATYPE_USERNAME, password: SONATYPE_PASSWORD)
                    }

                    pom.project {
                        name project.name
                        packaging 'jar'
                        description project.description

                        url 'https://github.com/IBM/reactive-components/' + project.name
                        scm {
                            connection 'scm:git:https://github.com/IBM/reactive-components/' + project.name + '.git'
                            developerConnection 'scm:git:git@github.com/IBM/reactive-components/' + project.name + '.git'
                            url 'https://github.com/IBM/reactive-components/' + project.name + '.git'
                        }

                        licenses {
                            license {
                                name 'Apache License 2.0'
                                url 'https://www.apache.org/licenses/LICENSE-2.0'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'FcoJavierSainz'
                                name = 'Javier Sainz'
                                email = 'fco.javier.sainz@hotmail.com'
                            }
                        }
                    }
                }
            }
        }
    }
}
